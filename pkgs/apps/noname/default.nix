{
  lib,
  stdenv,
  fetchPnpmDeps,
  pnpmConfigHook,
  makeWrapper,
  nodejs_24,
  pnpm_10,
  sources,
}:
let
  p = sources.noname;
in
stdenv.mkDerivation (finalAttrs: {
  pname = "noname";
  inherit (p) version src;

  pnpmRoot = ".";

  nativeBuildInputs = [
    nodejs_24
    pnpm_10
    pnpmConfigHook
    makeWrapper
  ];

  pnpmDeps = fetchPnpmDeps {
    inherit (finalAttrs)
      pname
      version
      src
      pnpmRoot
      ;
    pnpm = pnpm_10;
    fetcherVersion = 3;
    hash = "sha256-H2vYuaMY2w7MbHf934t7lTAQXf7rb0xW3+2ZhK83zjg=";
  };

  pnpmInstallFlags = [ "--frozen-lockfile" ];

  buildPhase = ''
    runHook preBuild

    cd ${finalAttrs.pnpmRoot}
    pnpm build

    runHook postBuild
  '';

  installPhase = ''
    runHook preInstall

    mkdir -p $out/share/noname
    cp -r ./* $out/share/noname

    mkdir -p $out/bin

    makeWrapper ${lib.getExe nodejs_24} $out/bin/noname-server \
            --add-flags "$out/share/noname/packages/fs/dist/entry.cjs" \
            --add-flags "--server" \
            --add-flags "--dirname=$out/share/noname/dist"

    runHook postInstall
  '';

  meta = with lib; {
    description = "无名杀 noname (libnoname/noname)";
    homepage = "https://github.com/libnoname/noname";
    license = licenses.gpl3Only;
    platforms = platforms.linux ++ platforms.darwin;
    sourceProvenance = [ sourceTypes.fromSource ];
  };
})
